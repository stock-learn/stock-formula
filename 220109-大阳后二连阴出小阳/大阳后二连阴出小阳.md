# 大阳后二连阴出小阳

点击 [这里](./大阳后二连阴出小阳-案例.md) 查看案例回测记录。

## 1. 修订历史

| 版本 |    日期    | 备注
|:----:|:----------:|:----:
| 1.0  | 2022-01-09 | 

> 公式起源： 002022-科华生物（211231~220107）

## 2. 胜率特点

- 大阳涨幅>=6%叠加一阳穿二均 - 做多意愿较强
- 20 日均线不向下 - 中期非下降趋势
- 大阳实体中位线二连阴跌破后小阳立即收回 - 空方发力跌破大阳中位线，但半路立即被多方顶回，弱不下去，原有趋势未破。
- 大概率会买在向上突破的起点

## 3. 操盘策略

- 进场策略： 小阳当日尾盘30分钟内买入，尽量在尾盘五分钟内买进
- 止损策略： MAX(2%，小阳低点, 连阴低点)，即最小止损 2%，最大止损为二连阴低点
    > 以小阳低点止损很容易被洗出去！
- 止盈策略：
    1. 大阳及其后一日的最高点
    2. 任一日 11:15~13:15 急速拉升者无条件择机止盈清仓避险。
        > 如果收盘证明急拉没有打回原型而是高位横盘，收盘可接回。
    3. 进场后若出现大阳，则保护大阳中位线。
    4. 进场后若出现小阳，则保护小阳低点或前一根大阳的中位线。
- 强制离场策略： 进场后2天内不触发止损或止盈，强制清仓离场


## 4. 选股公式

参数 N>=3，默认 3，用于当日尾盘进场，设置为大于3的值用于回测。

```
{N日前大阳，涨幅>=6%}
DY:=(REF(C,N)-REF(O,N))/REF(C,N+1)>=0.06;
{大阳实体中位股价}
DYZW:=(REF(C,N)+REF(O,N))/2;

{大阳后阴调两天}
EY:=LAST(CLOSE<OPEN,N-1,N-2)

   {阴调低点破大阳中位但不破大阳开盘}
   AND REF(L,N-2)<=DYZW
   AND REF(L,N-2)>=REF(O,N);

{N-3日为小阳，涨幅<=2%}
XY:= REF(O,N-3)<REF(C,N-3)
   AND (REF(C,N-3)-REF(O,N-3))/REF(C,N-2)<=0.02
   {小阳非高开}
   AND REF(O,N-3)<=REF(C,N-2)
   {小阳不创新低}
   AND REF(L,N-3)>=REF(L,N-2)
   {小阳开盘不能高于大阳实体中位线--避免高位接盘}
   AND REF(O,N-3)<=DYZW;

{MA20不向下}
MA20ND:=MA(REF(C,N-3),20)>=MA(REF(C,N),20);

{大阳实体上穿MA5}
DYCMA5:=MA(REF(C,N),5)>=REF(O,N)
   AND MA(REF(C,N),5)<=REF(C,N);

{大阳实体上穿MA20}
DYCMA20:=MA(REF(C,N),20)>=REF(O,N)
   AND MA(REF(C,N),20)<=REF(C,N);

{小阳实体上穿MA5--站上MA5了}
XYCMA5:=MA(REF(C,N-3),5)>=REF(O,N-3)
   AND MA(REF(C,N-3),5)<=REF(C,N-3);

{N-1~N-3为连续缩量}
SL:=LAST(V<=REF(V,1),N-1,N-2);

{N-1~N-3内有MA5金叉MA20}
JC:=EXISTR(CROSS(MA(C,5),MA(C,20)),N-1,N-3);

{C1. 基本型}
C1:=DY AND EY AND XY AND MA20ND;

{C2. C1叠加大阳实体上穿MA5}
C2:=C1 AND DYCMA5;

{C3. C2叠加大阳实体上穿MA20--大阳穿两均}
C3:=C2 AND DYCMA20;

{C4. C3叠加小阳实体上穿MA5}
C4:=C3 AND XYCMA5;

{C5. C4叠加连续缩量}
C5:=C4 AND SL;

{C6. C5叠加均线出现金叉}
C6:=C5 AND JC;


C5;
```

选等小阳公式：


```
{N日前大阳，涨幅>=6%}
DY:=(REF(C,N)-REF(O,N))/REF(C,N+1)>=0.06;
{大阳实体中位股价}
DYZW:=(REF(C,N)+REF(O,N))/2;

{大阳后阴调两天}
EY:=LAST(CLOSE<OPEN,N-1,N-2)
   {阴调低点破大阳中位但不破大阳开盘}
   AND REF(L,N-2)<=DYZW
   AND REF(L,N-2)>=REF(O,N);

{MA20不向下}
MA20ND:=MA(REF(C,N-2),20)>=MA(REF(C,N),20);

{大阳实体上穿MA5}
DYCMA5:=MA(REF(C,N),5)>=REF(O,N)
   AND MA(REF(C,N),5)<=REF(C,N);

{大阳实体上穿MA20}
DYCMA20:=MA(REF(C,N),20)>=REF(O,N)
   AND MA(REF(C,N),20)<=REF(C,N);

{N-1~N-2为连续缩量}
SL:=LAST(V<=REF(V,1),N-1,N-2);

{C1. 基本型}
C1:=DY AND EY AND MA20ND;

{C2. C1叠加大阳实体上穿MA5}
C2:=C1 AND DYCMA5;

{C3. C2叠加大阳实体上穿MA20--大阳穿两均}
C3:=C2 AND DYCMA20;

{C4. C3叠加连续缩量}
C4:=C3 AND SL;


C4;
```
