[返回](README.md)

# RS-常规指标集

适用于给其它公式引用的常规指标合集，使用 `"RS.指标名称"` 方式引用相应的指标值。

## 1. 可用的指标名称

| 序号 | 指标名称 | 值  | 用途                           |  
|------|----------|-----|--------------------------------|
| 市场类
| 1-1  | ST       | 0,1 | 是否是ST股                     |
| 1-2  | HS       | 0,1 | 是否是沪深两市                 |
| 1-3  | HSZB     | 0,1 | 是否是沪深A股主板              |
| 1-4  | HSZBST   | 0,1 | 是否是沪深A股主板ST股          |
| 1-5  | SZ       | 0,1 | 是否是深交所(深市)             |
| 1-6  | SZST     | 0,1 | 是否是深交所(深市)ST股         |
| 1-7  | SZZB     | 0,1 | 是否是深交所(深市)A股主板      |
| 1-8  | SZZBST   | 0,1 | 是否是深交所(深市)A股主板ST股  |
| 1-9  | SZCYB    | 0,1 | 是否是深交所(深市)创业板股     |
| 1-10 | SZCYBST  | 0,1 | 是否是深交所(深市)创业板ST股   |
| 1-11 | SH       | 0,1 | 是否是上交所(沪市)             |
| 1-12 | SHST     | 0,1 | 是否是上交所(沪市)ST股         |
| 1-13 | SHZB     | 0,1 | 是否是上交所(沪市)A股主板      |
| 1-14 | SHZBST   | 0,1 | 是否是上交所(沪市)A股主板ST股  |
| 1-15 | SHKCB    | 0,1 | 是否是上交所(沪市)科创板股     |
| 1-16 | SHKCBST  | 0,1 | 是否是上交所(沪市)科创板ST股   |
| 1-17 | BJ       | 0,1 | 是否是北交所                   |
| 1-18 | BJA      | 0,1 | 是否是北交所A股                |
| 涨跌停
| 2-1  | ZDF      | 0.1,0.2,0.3,- | 涨跌幅               |
| 2-2  | ZTJ      | P,-           | 涨停价               |
| 2-3  | ZT       | 0,1           | 是否涨停             |
| 2-4  | ZTY      | 0,1           | 是否一字涨停         |
| 2-5  | ZTT      | 0,1           | 是否T字涨停          |
| 2-6  | ZTN      | N,-           | 最近涨停距当前周期数 |
| 2-7  | DTJ      | P,-           | 跌停价               |
| 2-8  | DT       | 0,1           | 是否跌停             |
| 2-9  | DTY      | 0,1           | 是否一字跌停         |
| 2-10 | DTT      | 0,1           | 是否倒T字跌停        |
| 2-11 | DTN      | N,-           | 最近跌停距当前周期数 |
| 2-12 | DTB      | 0,1           | 是否地天板           |
| 2-13 | TDB      | 0,1           | 是否天地板           |

备注：

- 值 - 代表无效值 `DRAWNULL`。
- 值 P 代表计算出的实际价格。
- 值 N 代表计算出的距当前周期数。
- 涨跌幅规则：（不考虑上市或重新开市的无涨跌幅限制）
    1. 沪深主板：
        1. 非 ST 为 10%（`>= 1996-12-19`）。
        2. ST `< 1998-04-27` 为 10%。
        3. ST `>= 1998-04-27` 为 5%。
    2. 深市创业板：
        1. 非 ST `< 2020-08-24` 为 10%。
        2. 非 ST `>= 2020-08-24` 为 20%。
        3. ST `< 2020-09-13` 为 5%。
        4. ST `>= 2020-09-13` 为 20%。
    3. 沪市科创板为 20%（不区分 ST）。
    4. 北交所A股为 30%（不区分 ST）。
- `DTB|TDB` `是否地天板|天地板` 是根据`最高价|最低价`是否触及`涨停跌停价`来判断的。
- 直接使用该指标，会在主图左上角显示警告字眼。

**特别注意：** 因暂时无函数可判断个股什么时候发生 ST 转折，故涨跌幅指标仅适用于当前状态下的计算，历史 ST 判断、历史涨跌幅计算对有 ST 转折的个股是错误的。

## 2. 股票涨跌幅知识点

1. 沪深两市自 1996 年 12 月 16 日起涨跌幅限制为 10%。
2. 沪深两市自 1998 年 4 月 27 日起建立 ST 制度(风险警示股票)，ST 股票涨跌幅限制为 5%。
3. 深市创业板于 2009 年 10 月 23 日正式上市，实行 10% 涨跌幅限制。
4. 沪市科创板 2019 年 7 月 22 日首批公司上市(注册制试点)，实行 20% 涨跌幅限制。
5. 深市创业板于 2020 年 8 月 24 日实行 20% 涨跌幅限制，同日创业板注册制首批企业挂牌上市。
6. 深市创业板于 2020 年 9 月 13 日起 ST 股涨跌幅限制由 5% 改为 20% (首批 3 只 ST 3000029ST天龙、300446ST乐凯、300367ST网力 于 9 月 15 日出现)。
7. 深市 2021 年 4 月 6 日主板和中小板合并正式实施，“两个统一、四个不变”。
8. 北交所于 2021 年 11 月 15 日正式开市，实行 30% 涨跌幅限制。
9. 沪市科创板首只 `*ST` 股 688309 出现于 2022 年 1 月 24 日，保留 20% 涨跌幅限制不变。
10. 沪市科创板首只退市股 688086 出现于 2022 年 5 月 6 日，保留 20% 涨跌幅限制不变。

## 3. 股票代码知识点

1. 沪市 A 股代码前三位：主板 600、601、603、605，科创板 688。
2. 深市 A 股代码前三位：主板 000、001、002、003，创业板 300、301。
3. 北交所代码一般是以 8 字开头的：
   - 其中 82 开头的股票表示优先股，83 和 87 开头的股票表示普通股票，88 开头的股票表示公开发行的。
   - 4 开头的股票是三板市场上的股票，已经从主板退市，散户很难买卖到 4 开头的。老三板（A 股）股票是 400 开头的，B 股老三板股票是 420 开头的，新三板股票是 430 开头的。

## 4. 公式代码

```tdx
{主图左上角提示指标适用范围}
DRAWTEXT_FIX(ISLASTBAR,0,0,0,'此为公用指标集，仅用于其它指标作公式引用！'),COLORRED;


{====1. 市场类}
{1. 是否是ST股}
ST:NAMEINCLUDE('ST'),NODRAW;
{2. 是否是沪深两市}
HS:SETCODE=0 OR SETCODE=1,NODRAW;
{3. 是否是沪深A股主板}
HSZB:FINANCE(3)=1,NODRAW;
{4. 是否是沪深A股主板ST股}
HSZBST:HSZB AND ST,NODRAW;

{5. 是否是深交所(深市)}
SZ:SETCODE=0,NODRAW;
{6. 是否是深交所(深市)ST股}
SZST:SZ AND ST,NODRAW;
{7. 是否是深交所(深市)A股主板}
SZZB:SZ AND FINANCE(3)=1,NODRAW;
{8. 是否是深交所(深市)A股主板ST股}
SZZBST:SZZB AND ST,NODRAW;
{9. 是否是深交所(深市)创业板}
SZCYB:FINANCE(3)=3,NODRAW;
{10. 是否是深交所(深市)创业板ST股}
SZCYBST:SZCYB AND ST,NODRAW;

{11. 是否是上交所(沪市)}
SH:SETCODE=1,NODRAW;
{12. 是否是上交所(沪市)ST股}
SHST:SH AND ST,NODRAW;
{13. 是否是上交所(沪市)A股主板}
SHZB:SH AND FINANCE(3)=1,NODRAW;
{14. 是否是上交所(沪市)A股主板ST股}
SHZBST:SHZB AND ST,NODRAW;
{15. 是否是上交所(沪市)科创板}
SHKCB:FINANCE(3)=4,NODRAW;
{16. 是否是上交所(沪市)科创板ST股}
SHKCBST:SHKCB AND ST,NODRAW;

{17. 是否是北交所}
BJ:SETCODE=2,NODRAW;
{18. 是否是北交所A股}
BJA:FINANCE(3)=2,NODRAW;


{====2. 涨跌停类}
{1. 涨跌幅}
ZDF:IF(
  {5%: 沪深主板ST或<2020-09-13的创业板ST}
  HSZBST OR (SZCYBST AND DATE<1200913), 0.05,
  {10%: 沪深主板或<2020-08-24的创业板}
  IF(HSZB OR (SZCYB AND DATE<1200824),  0.1,
  {20%: 科创板或>=2020-08-24的创业板}
  IF(SHKCB OR (SZCYB AND DATE>=1200824),0.2,
  {30%: 北交所}
  IF(BJA,                               0.3,
  {其它情况返回无效值}
  DRAWNULL)))
),NODRAW;
{2. 涨停价}
ZTJ:IF(PERIOD=5,ZTPRICE(REF(C,1),ZDF),DRAWNULL),NODRAW;
{3. 是否涨停}
ZT:C>=ZTJ AND C=H,NODRAW; {用>=而不是用=可以解决部分复权数据的计算误差问题}
{4. 是否一字涨停}
ZTY:ZT AND C=O AND C=L,NODRAW;
{5. 是否T字涨停}
ZTT:ZT AND C=O AND C>L,NODRAW;
{6. 最近涨停距当前周期数}
ZTN:BARSLAST(ZT),NODRAW;

{7. 跌停价}
DTJ:IF(PERIOD=5,DTPRICE(REF(C,1),ZDF),DRAWNULL),NODRAW;
{8. 是否跌停}
DT:C<=DTJ AND C=L,NODRAW;
{9. 是否一字跌停}
DTY:DT AND C=O AND C=H,NODRAW;
{10. 是否倒T字跌停}
DTT:DT AND C=O AND C<H,NODRAW;
{11. 最近跌停距当前周期数}
DTN:BARSLAST(DT),NODRAW;

{12. 是否地天板}
DTB:ZT AND L<=DTJ,NODRAW;
{13. 是否天地板}
TDB:DT AND H>=ZTJ,NODRAW;
```

用法注释：

```txt
常规指标集
Since 2022-10-01 @RJ <rongjih@163.com>
```

## 5. 下载

[RS-常规指标集-v221005.tn6](assets/11-RS-常规指标集-v221005.tn6)
