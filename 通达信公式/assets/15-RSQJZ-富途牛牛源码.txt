{NINE-神奇九转
指标简介
    神奇九转指标思想来源于技术分析领域著名大师汤姆·迪马克的TD序列，其核心功能为发现当前股价走势的拐点，提高抄底、逃顶的成功率。
指标逻辑 
    股价在上涨或（下跌）过程中连续9日达到触发条件会生成数列1、2、3、4、5、6、7、8、9，数列会依次标注在当日K线上方（下方）。只有当股价连续第六天达到触发条件时，数列才开始进行显示，依次显示1、2、3、4、5、6，当第七天依然达到触发条件时则显示7，如第七日未达到触发条件则前面6天的序号消失。第八日同第七日的显示逻辑一样。当第九天依然达到触发条件时，便形成了一个九转结构。而当第九日未达到触发条件时则前面8日的序号消失，九转结构不成立。股价上涨过程中形成的九转结构称之为上涨9结构，数字在k线上方，而股价下跌过程中形成的九转结构则称之为下跌9结构，数字在k线下方。
指标用法
    神奇九转指标最核心的用法，就是帮助我们有效的逃离股票的顶部区域，精准把握股票的底部机会。当股价在运行过程中出现上涨9结构时，此时股价往往位于顶部反转区域，个股大概率面临反转下跌的风险，此时我们需要降低仓位规避风险。而当股价出现下跌9结构时，股价往往处于底部反转区域，个股大概率开启止跌反弹的走势，此时可考虑建仓或加仓，等待获利时机。
}

{参数}
N:=(0,1000,15);

A1:=C>REF(C,4);
NT:=BARSLASTCOUNT(A1);
TJ11:=NT=9;
TJ13:=ISLASTBAR AND BETWEEN(NT,5,8);
AY:=(BACKSET(TJ11>0,9) OR BACKSET(TJ13>0,NT))*NT;
DRAWNUMBER((AY > 0 AND AY < 9), H, AY, 0, N), COLORFF00FF;
DRAWNUMBER(NT = 9, H, 9, 0, N), COLORGREEN;

B1:=C<REF(C,4);
NT0:=BARSLASTCOUNT(B1);
TJ21:=NT0=9 ;
TJ23:=ISLASTBAR AND BETWEEN(NT0,5,8);
AY1:=(BACKSET(TJ21>0,9) OR BACKSET(TJ23>0,NT0))*NT0;
DRAWNUMBER((AY1 > 0 AND AY1 < 9), L, AY1, 0, -1*N), COLORGREEN;
DRAWNUMBER(NT0 = 9, L, 9, 0, -1*N), COLORFF00FF;